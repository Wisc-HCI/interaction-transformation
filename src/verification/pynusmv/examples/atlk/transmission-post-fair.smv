--- One bit transmission system
---
--- The system is composed of one bit of information, a sender
--- and a transmitter.
--- The sender can ask to send the bit or to wait, while the transmitter can
--- choose to transmit the bit or block the transmission.
--- The bit is received if and only if the sender sends the bit,
--- and the transmitter transmits it.
---
--- In this version of the system, fairness constraints are added such that
--- a fair path only happens when the transmitter allows the transmission
--- infinitely often (representing a fair channel).
---
--- In this version of the system, additional state variables are added to
--- track the last played actions, and fairness constraints are only specified
--- on state variables (not on input variables).
---
--- Simon Busard <simon.busard@uclouvain.be>, 23/03/13

MODULE Sender()

    IVAR   action : {send, wait};
    
    --- Protocol: nothing, can always do both actions


MODULE Transmitter()
    
    IVAR action : {transmit, block};
    
    --- Protocol: nothing, can always do both actions
                

MODULE main

    VAR received : boolean;
        sender : Sender();
        transmitter : Transmitter();
        sent : boolean;
        waited : boolean;
        transmitted : boolean;
        blocked : boolean;
        
    INIT !received & !sent & !waited & !transmitted & !blocked
    
    TRANS next(received) = (sender.action = send & transmitter.action = transmit
                            ? TRUE : received);
                            
    TRANS next(sent) = (sender.action = send)                        
    TRANS next(waited) = (sender.action = wait)
    TRANS next(transmitted) = (transmitter.action = transmit)
    TRANS next(blocked) = (transmitter.action = block)
                            

FAIRNESS transmitted